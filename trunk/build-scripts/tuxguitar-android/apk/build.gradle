apply plugin: 'com.android.application'

def plugins = [
	'TuxGuitar-android-gervill-plugin',
	'TuxGuitar-android-midi-plugin',
	'TuxGuitar-android-browser-saf-plugin',
	'TuxGuitar-android-gdrive-plugin',
	'TuxGuitar-compat',
	'TuxGuitar-gtp',
	'TuxGuitar-gpx',
	'TuxGuitar-ptb',
	'TuxGuitar-midi',
	'TuxGuitar-lilypond',
	'TuxGuitar-pdf',
]

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"
    
    defaultConfig {
        applicationId "org.herac.tuxguitar.android.application"
        minSdkVersion 16
        targetSdkVersion 28
        multiDexEnabled true
        versionCode Integer.parseInt(new Date().format('yyMMddHH'))
        versionName "SNAPSHOT"
        setProperty("archivesBaseName", "tuxguitar-android-SNAPSHOT")
    }
	
    buildTypes {
        release {
        	zipAlignEnabled true
        }
    }
    
    dexOptions {
        additionalParameters=['--core-library']
    }
	
    sourceSets {
        main {
        	java.srcDirs = ['./build/generated/tuxguitar-android-apk-src/src']
			resources.srcDirs = ['./build/generated/tuxguitar-android-apk-src/share']
        }
    }
}

task generateSources {
 	// copy plugins to tmp folder
    plugins.each { String plugin ->
	    copy {
	        from '../../../' + plugin
	        into './build/generated/tuxguitar-android-apk-src'
	        
	        exclude '**/META-INF/services/org.herac.tuxguitar.util.plugin.TGPlugin'
	    }
    }
    
    // create unified /META-INF/services/org.herac.tuxguitar.util.plugin.TGPlugin file
	List<String> fileNames = new ArrayList<String>()
    plugins.each { String plugin ->
        fileNames.add('../../../' + plugin + '/share/META-INF/services/org.herac.tuxguitar.util.plugin.TGPlugin')
    }
	File file = new File('apk/build/generated/tuxguitar-android-apk-src/share/META-INF/services/org.herac.tuxguitar.util.plugin.TGPlugin');
 	file.getParentFile().mkdirs()
 	file.text = files(fileNames).collect{it.getText()}.join("\n")
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.multidex:multidex:2.0.0'
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation project(':tuxguitar-android')
    implementation project(':tuxguitar-android-resources')
    implementation project(':tuxguitar-android-gervill')
    implementation project(':tuxguitar-android-midi')
    implementation project(':tuxguitar-android-browser-saf')
    implementation project(':tuxguitar-android-gdrive')
    implementation project(':tuxguitar-android-gervill-soundbank')
    
    implementation 'com.itextpdf:itextpdf:5.5.10'
    implementation 'com.itextpdf.tool:xmlworker:1.1.1'
    implementation 'org.apache.commons:commons-compress:1.5'
}
